apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply plugin: 'project-report'
apply plugin: 'eclipse'
apply plugin: 'findbugs'
apply plugin: 'pmd'
apply plugin: 'build-dashboard'

defaultTasks 'all'

task all (dependsOn: [projectReport, build, ]) {
    finalizedBy buildDashboard
}

check.dependsOn = [
    test,
    jacocoTestReport,
    findbugsMain,
    pmdMain,
]

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    testCompile 'junit:junit:4.11'
    testCompile 'org.mockito:mockito-core:1.9.5'
}

task sourcesJar(type: Jar, dependsOn:classes) {
    description = 'The source for this module for use in IDEs'
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn:javadoc) {
    description = 'The javadoc for this module for use in IDEs'
    classifier = 'javadoc'
    from javadoc.destinationDir
}

publish {
    dependsOn check
}

publishing {
    publications {
        main(MavenPublication) {
            from components.java
            artifact sourcesJar {
                classifier 'sources'
            }
            artifact javadocJar {
                classifier 'javadoc'
            }
        }
    }
    repositories {
        maven {
            url "http://www.hjug.org/libs-snapshost-local"
            credentials {
                username = fakeUserName
                password = fakeUserPassword
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.7'
}

eclipse {
    classpath {
        defaultOutputDir = file('eclipseBin')
    }
}
